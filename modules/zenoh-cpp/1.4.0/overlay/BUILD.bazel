# Copyright 2025 Open Source Robotics Foundation, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load("@rules_cc//cc:defs.bzl", "cc_library", "cc_test")

package(default_visibility = ["//visibility:public"])

cc_library(
    name = "zenoh-cpp",
    hdrs = glob(["include/**"]),
    includes = ["include"],
    defines = ["ZENOHCXX_ZENOHC"],
    cxxopts = ["-Wsign-compare"],
    deps = ["@zenoh-c"],
)

# Tests that may be parallelized.

[
    cc_test(
        name = name.replace("/", "_") + "_test",
        srcs = ["tests/" + name + ".cxx"],
        cxxopts = ["-Wno-parentheses", "-Wno-sign-compare","-Wno-unused-variable"],
        deps = [":zenoh-cpp"],
        timeout = "short",
        tags = ["requires-network"]
    ) for name in [
        "build/warnings",
        "universal/bytes",
        "universal/closures",
        "universal/details",
        "universal/serialization",
        "zenohc/advanced_pub_sub",
        "zenohc/config",
        "zenohc/shm_api",
    ]
]

# Tests that must be run with network in isolation.

[
    cc_test(
        name = name.replace("/", "_") + "_test",
        srcs = ["tests/" + name + ".cxx"],
        cxxopts = ["-Wno-parentheses", "-Wno-sign-compare","-Wno-unused-variable"],
        deps = [":zenoh-cpp"],
        timeout = "short",
        tags = ["requires-network", "exclusive"]
    ) for name in [
        "universal/network/keyexpr",
        "universal/network/liveliness",
        "universal/network/pub_sub",
        "universal/network/queryable_get",
    ]
]