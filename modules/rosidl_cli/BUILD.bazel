
# Copyright 2025 Open Source Robotics Foundation, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load("@ros//python:defs.bzl", "py_pytest")
load("@rules_python//python:defs.bzl", "py_library", "py_binary")
load("@rules_python//python/entry_points:py_console_script_binary.bzl", "py_console_script_binary")

# This is the python library that does the bulk of the work for converting
# the .msg, .srv, .action files to the generic .idl representation.
py_library(
    name = "rosidl_cli",
    srcs = glob(["rosidl_cli/**/*.py"]),
    deps = ["@ros//python:pyyaml"],
    visibility = ["//visibility:public"],
)

# This makes the setup script executable, and ensures that eny downstream
# consumers of this script bring in the correct dependencies.
py_binary(
    name = "setup",
    srcs = ["setup.py"],
    deps = ["@ros//python:setuptools"],
    visibility = ["//visibility:private"],
)

# This genrule runs setup.py to generate a entry_points.txt that contains
# amongst other things a list of console scripts.
genrule(
    name = "entry_points",
    srcs = glob(["**/**"], exclude=["*.bazel", "*.bzl"]),
    outs = ["rosidl_cli.egg-info/entry_points.txt"],
    cmd = "$(location :setup) egg_info --egg-base=$(@D)/..",
    tools = [":setup"],
    visibility = ["//visibility:private"],
)

# Extract the rosidl console script to use by others.
py_console_script_binary(
    name = "rosidl",
    pkg = ":rosidl_cli",
    entry_points_txt = ":entry_points",
    script = "rosidl",
    visibility = ["//visibility:public"],
)

# All functional tests.
[
    py_pytest(
        name = test,
        srcs = ["test/rosidl_cli/{}.py".format(test)],
        python_version = "PY3",
        data = ["test/rosidl_cli/test_files/bar/msg/Bar.idl"],
        deps = [":rosidl_cli"],
    )
    for test in ["test_common", "test_extensions", "test_helpers"]
]
